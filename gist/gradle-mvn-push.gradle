apply plugin: 'maven-publish'

def isReleaseBuild() {
    return System.getenv("GITHUB_REF_TYPE") == "tag"
}

def getVersionName() {
    if (isReleaseBuild()) {
        return System.getenv("GITHUB_REF_NAME")
    } else {
        if (System.getenv("GITHUB_REF_NAME") != "develop") {
            String branch = System.getenv("GITHUB_REF_NAME").replaceAll("[^a-zA-Z0-9]+","")
            return "${branch}-dev"
        } else {
            return "develop-dev"
        }
    }
}

def getArtifactFilePath() {
    if (isReleaseBuild()) {
        return "build/outputs/aar/${project.artifactId}-release.aar"
    } else {
        return "build/outputs/aar/${project.artifactId}-debug.aar"
    }
}

def getPublicationName() {
    if (isReleaseBuild()) {
        return "release"
    } else {
        return "debug"
    }
}

publishing {
    publications {
        "${getPublicationName()}" (MavenPublication) {
            groupId project.groupId
            artifactId project.artifactId
            version getVersionName()
            artifact getArtifactFilePath()

            // To include project dependencies
            pom.withXml {
                def dependencies = asNode().appendNode('dependencies')
                configurations.getByName("${getPublicationName()}CompileClasspath").getResolvedConfiguration().getFirstLevelModuleDependencies().each {
                    def dependency = dependencies.appendNode('dependency')
                    dependency.appendNode('groupId', it.moduleGroup)
                    dependency.appendNode('artifactId', it.moduleName)
                    dependency.appendNode('version', it.moduleVersion)
                }
            }
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/customerio/gist-android"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}